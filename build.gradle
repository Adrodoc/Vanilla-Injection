buildscript {
  repositories { jcenter() }

  dependencies { classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0' }
}
apply plugin: 'com.github.ben-manes.versions'

ext {
  productName = 'Vanilla-Injection'
  encoding = 'UTF-8'
}
allprojects {
  group = 'com.energyxxer.inject'
  version = getGitVersion()
}

String getGitVersion() {
  try {
    def stdout = new ByteArrayOutputStream()
    exec {
      commandLine 'git', 'describe', '--dirty=-SNAPSHOT'
      standardOutput = stdout
    }
    return stdout.toString().trim().substring(1)
  } catch (Exception e) {
    return "UNKNOWN-VERSION"
  }
}

boolean isReleaseVersion() {
  version ==~ /\d+\.\d+\.\d+/
}

// ----------------------------------------------------------------------------------------------------
//    _      _  _                        _
//   | |    (_)| |__   _ __  __ _  _ __ (_)  ___  ___
//   | |    | || '_ \ | '__|/ _` || '__|| | / _ \/ __|
//   | |___ | || |_) || |  | (_| || |   | ||  __/\__ \
//   |_____||_||_.__/ |_|   \__,_||_|   |_| \___||___/
//
// ----------------------------------------------------------------------------------------------------

ext.libs = [
  guava:          'com.google.guava:guava:23.0',
  jsr305:         'com.google.code.findbugs:jsr305:3.0.2',
  nbt:            'com.evilco.mc:nbt:1.0.2',
  log4j_api:      'org.apache.logging.log4j:log4j-api:2.9.1',
  log4j_core:     'org.apache.logging.log4j:log4j-core:2.9.1'
]

// ----------------------------------------------------------------------------------------------------

task wrapper (type: Wrapper) { gradleVersion = '4.3' }

allprojects {
  repositories {
    mavenCentral()
    maven { url 'http://basket.cindyscats.com/content/repositories/releases' }
  }

  pluginManager.withPlugin('eclipse') {
    apply from: rootProject.file('snippets/eclipse/configure-eclipse.gradle')
  }

  tasks.withType(JavaCompile) { options.encoding = encoding }

  tasks.withType(Test) {
    beforeTest { TestDescriptor descriptor ->
      logger.lifecycle('Running test: ' + descriptor.name + '(' + descriptor.className + ')')
    }
  }
}

// ----------------------------------------------------------------------------------------------------

apply plugin: 'java'
apply plugin: 'eclipse'

archivesBaseName = 'vanilla-injection'

// ----------------------------------------------------------------------------------------------------
//    ____                                    ____         _
//   / ___|   ___   _   _  _ __  ___  ___    / ___|   ___ | |_  ___
//   \___ \  / _ \ | | | || '__|/ __|/ _ \   \___ \  / _ \| __|/ __|
//    ___) || (_) || |_| || |  | (__|  __/    ___) ||  __/| |_ \__ \
//   |____/  \___/  \__,_||_|   \___|\___|   |____/  \___| \__||___/
//
// ----------------------------------------------------------------------------------------------------

sourceSets {
  main.java {
    srcDir 'Injection Demo/src'
    srcDir 'Vanilla Injection/src'
  }
}

// ----------------------------------------------------------------------------------------------------
//    ____                                 _                     _
//   |  _ \   ___  _ __    ___  _ __    __| |  ___  _ __    ___ (_)  ___  ___
//   | | | | / _ \| '_ \  / _ \| '_ \  / _` | / _ \| '_ \  / __|| | / _ \/ __|
//   | |_| ||  __/| |_) ||  __/| | | || (_| ||  __/| | | || (__ | ||  __/\__ \
//   |____/  \___|| .__/  \___||_| |_| \__,_| \___||_| |_| \___||_| \___||___/
//                |_|
// ----------------------------------------------------------------------------------------------------

dependencies {
  compileOnly libs.jsr305

  compile libs.guava
  compile libs.nbt
  compile libs.log4j_api

  runtime libs.log4j_core
}
